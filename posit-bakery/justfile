#!/usr/bin/env just --justfile

# Fallback to parent justfile(s)
set fallback

################
# Variables
CWD := justfile_directory()

################
# Default command, must be first in the file
[private]
_default:
    just --list --unsorted

################
# Setup commands

# Set up local dev environment
setup:
    just _setup-poetry

[private]
_setup-poetry:
    poetry --version || just _python-executable poetry

################
# Commands

# Install the posit-bakery python project
install:
    poetry install \
        --directory={{ CWD }}

alias package := build
# Build the posit-bakery python project
build:
    poetry build \
        --directory={{ CWD }} \
        --clean

[private]
_pytest *ARGS:
    poetry run -- pytest \
        -n auto \
        --cov=posit_bakery \
        --cov-report=term-missing \
        {{ ARGS }} \
        {{ CWD }}/test/

# Note: Both sets of quotes are required
# Test the posit-bakery python project, skipping slow tests
test *ARGS:
    just _pytest {{ ARGS }} -m "'not slow'"

# Run all tests
test-all *ARGS:
    just _pytest {{ ARGS }}
